
# using vars: install_filter

label mr_problem:
	scene bg my_room with dissolve
	play music music_list['usual'] fadein 1
	play sound sfx_list['message']
	"Проснулся я от сообщения Маширо."
	mr "Завтракать я сегодня не буду."
	"Ну, не хочет - не надо. Нам больше достанется."
	
	play sound sfx_list['message']
	"После завтрака я вновь получил от неё сообщение."
	stop music fadeout 0.5
	pause 0.5
	play music music_list['running-1'] fadein 1
	mr "Срочно иди ко мне. И сестру прихвати."
	"Вот это уже было странно."
	"Что у неё там произошло?"
	"Мы бежали со всех ног и были у неё уже через минуту."
	
	scene bg ship-4 with dissolve
	stop music fadeout 0.5
	pause 0.5
	play music music_list['apathy-1'] fadein 1
	show mr serious with dissolve
	"Она ходила по комнате и явно была чем-то взволнована."
	
	mr "О, вы быстро, это нам на руку."
	mr "Присаживайтесь, а то, боюсь, вы упадёте через пару минут."
	mr "И это не шутка. {w}Всё очень серьёзно."
	
	"Мы с сестрой ничего не понимали, но на всякий случай послушались её."
	"Через пару секунд Маширо начала свою речь."
	
	show mr normal with dspr
	mr "У меня для вас несколько новостей."
	mr "Хотя в некотором роде это и для меня новости."
	mr "Начнём с начала."
	mr "Во первых, Рен была права, и я довольно опасна, ведь я пират."
	
	"Я не знаю, чего я больше испытывал в этот момент."
	"Неожиданность? {w}Предательство? {w}Страх? {w}Апатия? {w}Конец всем мечтам и надеждам?"
	"Наверное, всё вместе..."
	
	mr "Но не бойтесь, я не собираюсь вас грабить, калечить, убивать и всё в таком духе."
	mr "Разумеется, при условии, что вы не будете ни в чём мне мешать."
	mr "Если вам кажется, что вы всё ещё способны меня остановить - вам именно кажется, настоятельно советую не дёргаться."
	mr "В вашем завтраке было снотворное, так что находиться в сознании вам осталось не больше 10 минут."
	
	show mr sad with dissolve
	mr "После я помещу вас в спасательный модуль и оставлю на ближайшей планете с приемлимым климатом."
	"..."
	mr "Затем я улечу на этом корабле на свою базу, и автопилот доставит этот корабль обратно к вам, разумеется, не сохранив историю полёта."
	"..."
	mr "А потом вы сможете снова жить на нём и делать что хотите."
	"..."
	mr "Будто ничего и не было."
	"..."
	mr "И мы с вами вовсе не встречались."
	
	"Её лицо становилось всё более печальным с каждой произносимой фразой."
	"А паузы между ними становились всё больше..."
	"Я считал её предателем, но как же мне хотелось ей помочь!"
	"Помочь в...конфискации моего имущества со словами о дальнейшем возврате?"
	"Или в чём?"
	"Я окончательно запутался в своих эмоциях."
	"Или это уже не просто эмоции?"
	
	show mr normal with dspr
	mr "Особенно...{w}с тобой, Сэм."
	mr "Я...{w}я...{w}ладно, чего уж тут стесняться. {w}Раз уж это наши последние совместные минуты..."
	mr "Я люблю тебя, Сэм."
	me "Что? Меня...{w}но..."
	"Я думал, что ещё больше меня удивить уже невозможно, но ей, казалось, было под силу абсолютно всё."
	me "Почему...{w}и зачем тогда..."
	mr "Я полюбила тебя с первого взгляда."
	mr "Возможно, конечно, это из-за того, что ты был единственным человеком, которого я встретила за такое долгое время, но мне хотелось бы думать иначе."
	
	show mr sad with dissolve
	mr "Мне очень жаль, что так вышло."
	mr "Прости..."
	mr "По-другому и правда было нельзя."
	mr "Я...на самом деле, с самого начала я думала, что вы тоже пираты."
	mr "Это сейчас ваш корабль хуже среднего."
	mr "Я не думала, что наука в кораблестроении сделает такой прорыв менее чем за 2 года."
	mr "Раньше корабль с такими характеристиками стоил так дорого, что никому были не интересны его скорость и манёвреность, кроме пиратов."
	mr "Если бы я знала это с самого начала, я не попросила бы твоей помощи..."
	
	"Она подошла ко мне, села рядом и оперлась своим плечом о моё."
	mr "И сейчас...{w} я не могла просто попросить тебя отвести меня на мою базу."
	mr "Ведь показывать её местоположение строго запрещено."
	mr "Если там узнают о наличии посторонних, то нам конец."
	
	show mr normal with dissolve
	mr "И...должна тебе сказать, ты единственный торговец, к которому я не испытываю ненависти."
	mr "Меня ужасно бесят эти люди, что грызутся за каждую копейку и вечно ищут, где бы поспикулировать, почти не принося реальной пользы."
	mr "Но ты не из таких! Ты не раз привозил еды на голодающие планеты, работая почти себе в убыток."
	mr "Я...я знаю это, потому что взломала ваш бортовой компьютер."
	mr "В конце концов, я должна была быть уверена, что смогу полностью очистить его историю, так что прости меня и за это..."
	me "Да ладно...{w}В нём не было чего-то личного, так что взламывай себе на здоровье..."
	me "Ты тоже планируешь жить дальше как обычно?"
	
	show mr happy with dspr
	mr "Нет, у меня есть парочка технологий, с помощью которых я планирую захватить целую планету!"
	show mr normal with dissolve
	mr "Там у меня будет очень много дел. {w}И я смогу не так сильно мучаться из-за отсутствия тебя, как это было бы, живи я прежней жизнью."
	mr "Где-то через пол-года я закончу приготовления и начну сам захват. {w}Через год там всё более менее уляжется."
	"Она молчала некоторое время, пытаясь собраться с мыслями."
	mr "Я...понимаю, что с моей стороны это наверняка прозвучит сверхнагло, но..."
	mr "Я смогу наконец позвать тебя к себе."
	mr "Смогу найти тебя, пригласить и пожить с тобою...{w}хотя бы пару дней..."
	mr "Ты...ты не слишком сильно на меня злишься? {w}Ты согласишься прилететь ко мне ещё хотя бы раз?"
	
	menu:
		"Я полностью тебя понимаю и поддерживаю":
			show mr happy with dspr
			mr "Правда? И ты не сердишься, что я без спросу пытаюсь угнать ваш корабль?"
			me "Конечно, нет!"
			me "Ведь иначе тебе придётся умереть, а я бы не хотел, что бы что-то такое произошло с тобой."
			
			"Или от нервов, или от снотворного Рен не выдержала и потеряла сознание."
			"Я успел поймать её и положил на кровать."
			
			if not install_filter:
				"Я давно уже начал чувствовать, что меня постепенно клонит в сон."
				me "Обязательно пригласи, я с радостью погощу у тебя."
				scene bg black with dissolve
				mr "Обязательно."
				mr "Если останусь жива в той круговерти, что произойдёт."
				
				stop music fadeout 1
				pause 1
				jump mr_epilogue
			else:
				me "Слушай, прошло уже явно больше 10 минут..."
				me "Погоди-ка. Ты своё снотворное случайно не в систему питьевой воды добавила?"
				show mr surprise with dspr
				mr "А что?"
				me "У нас тут одна художница, которую ты сейчас жутко перепугала, недавно выиграла супер-новую систему очистки воды."
				
				show mr upset with dspr
				mr "Упс."
				mr "Но, в некотором роде, так даже лучше."
				mr "Мне бы не хотелось, что бы ты забыл что-то из нашего разговора или вообще посчитал бы всё это сном."
				mr "Но по ощущениям, я сейчас не проваливаюсь от стыда сквозь землю только потому, что тут нет земли."
				
				stop music fadeout 1
				pause 1
				jump mr_continue
		
		"После всего этого?":
			$ root_name = 'mr-bad'
			me "Да я видеть тебя больше не могу!"
			me "А ты ещё задаёшь тут такие глупые вопросы."
			show mr sad with dissolve
			mr "Жаль."
			mr "Что ж, я могу тебя понять."
			mr "Быть может, я бы и сама была бы того же мнения на твоём месте."
			
			if not install_filter:
				"Я давно уже начал чувствовать, что меня постепенно клонит в сон."
				"Рен спит уже как минимум 3 минуты."
				me "Надеюсь, мы больше никогда не встретимся...прощай."
				mr "Не беспокойся, всё будет так, как ты хочешь. {w}Ещё раз: прости, что всё так вышло."
				scene bg black with dissolve
				mr "И прощай."
				
				stop music fadeout 1
				pause 1
				jump mr_epilogue
			else:
				me "Слушай, прошло уже явно больше 10 минут..."
				me "Погоди-ка. Ты своё снотворное случайно не в систему питьевой воды добавила?"
				show mr surprise with dspr
				mr "А что?"
				me "У нас тут одна художница, которую ты сейчас жутко перепугала, недавно выиграла супер-новую систему очистки воды."
				
				show mr upset with dspr
				mr "Упс."
				mr "Но, в некотором роде, так даже лучше."
				mr "Мне бы не хотелось, что бы ты забыл что-то из нашего разговора или вообще посчитал бы всё это сном."
				
				"Тут у меня в голове появилась одна мысль."
				"Мой противник сидит рядом со мной и абсолютно беззащитен."
				menu:
					"Схватить, повязать и сдать куда следует":
						stop music fadeout 1
						pause 1
						jump mr_bad_end
					"Не рисковать лишний раз":
						stop music fadeout 1
						pause 1
						jump mr_epilogue_no_sleep

